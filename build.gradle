plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'java'
}

group = 'systems.kscott'
version = '5.0.0'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name "essentialsx-releases"
        url "https://repo.essentialsx.net/releases/"
    }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = "https://hub.spigotmc.org/nexus/content/groups/public/" }
    maven { url = "https://jitpack.io" }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.19-R0.1-SNAPSHOT'
    compileOnly 'org.projectlombok:lombok:1.18.24'
    compileOnly 'net.essentialsx:EssentialsX:2.19.4'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"
    implementation "io.papermc:paperlib:1.0.7"
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

shadowJar {
    relocate 'co.aikar.commands', 'systems.kscott.randomspawnplus.acf'
    relocate 'co.aikar.locales', 'systems.kscott.randomspawnplus.locales'
    relocate "io.papermc.lib", "systems.kscott.randomspawnplus.paperlib"
}

build.dependsOn shadowJar